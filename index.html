<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <title>Document</title>
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <!-- <script type="text/javascript" src="PlyReader.js"></script> -->
    <script type="text/javascript" src="utils/MV.js"></script>
    <script type="text/javascript" src="utils/BspTree.js"></script>
    <script type="text/javascript" src="utils/Aabb.js"></script>
    <script type="text/javascript" src="utils/helpers.js"></script>
    <script type="text/javascript" src="app.js"></script>
    
    <script id="wgsl" type="x-shader"> 

        const PI: f32 = 3.14159265359;
        const INFINITY: f32 = 1.0e+38;
        
        const TMIN: f32 = 0.0;
        const TMAX: f32 = 1000.0;
        const MAXINTERSECTIONS: i32 = 1;
        const BACKGROUNDCOLOR: vec4f = vec4f(0); // white

        // See: https://en.wikipedia.org/wiki/Table_of_spherical_harmonics
        const SH_C0 = 0.28209479177387814;
        const SH_C1 = 0.4886025119029199;
        const SH_C2 = array(
            1.0925484305920792,
            -1.0925484305920792,
            0.31539156525252005,
            -1.0925484305920792,
            0.5462742152960396
        );
        const SH_C3 = array(
            -0.5900435899266435,
            2.890611442640554,
            -0.4570457994644658,
            0.3731763325901154,
            -0.4570457994644658,
            1.445305721320277,
            -0.5900435899266435
        );

        const MAX_LEVEL: u32 = 20;
        const BSP_LEAF: u32 = 3;
        var<private> branch_node: array<vec2u, MAX_LEVEL>; 
        var<private> branch_ray: array<vec2f, MAX_LEVEL>;

        @group(0) @binding(0) var<uniform> uniforms : Uniforms;
        @group(0) @binding(1) var renderTexture: texture_2d<f32>;

        @group(0) @binding(2) var<storage, read> gaussians: array<Gaussian>;
        @group(0) @binding(3) var<storage, read> spherical_harmonics: array<array<vec3<f32>, 16>>;

        @group(0) @binding(4) var<uniform> aabb: AABB;
        @group(0) @binding(5) var<storage> tree_ids: array<u32>; 
        @group(0) @binding(6) var<storage> bsp_tree: array<vec4u>;
        @group(0) @binding(7) var<storage> bsp_planes: array<f32>;

        struct Uniforms {
            eps: f32,
            aspect_ratio: f32,
            gamma: f32,
            frame_number: f32,       // u32 in spirit, not possible in practice
            canvas_width: f32,       // u32 in spirit, not possible in practice
            canvas_height: f32,      // u32 in spirit, not possible in practice
            _padding1: f32,
            camera_const: f32,
            camera_position: vec3f,
            camera_look_point: vec3f,
            camera_up_vector: vec3f,
        };

        struct Gaussian {
            position: vec3f,
            scale: vec3f,
            rotation: vec4f,
            normal: vec4f,  // w component is the base alpha / opacity 
        };

        struct VSOut {
            @builtin(position) position: vec4f,
            @location(0) coords: vec2f
        };

        struct FSOut {
            @location(0) frame: vec4f, 
            @location(1) accum: vec4f
        };

        struct AABB {
            min: vec3f,
            max: vec3f
        };

        struct Ray {
            origin: vec3f,
            direction: vec3f,
            t_min: f32,
            t_max: f32
        };

        struct HitInfo {
            has_hit: bool, 
            dist: f32, 
            position: vec3f, 
            normal: vec3f, 
            material: Material,
            entry: vec3f,
            exit: vec3f,
            gaussian: Gaussian
        };

        struct Sphere {
            center: vec3f,
            radius: f32,
            material: Material
        };
                
        struct Material {
            color: vec4f,
            emission: vec3f,
            specular: vec3f,
            index_of_refraction: f32,
            specular_reflectance: f32,
            shininess: f32
        };

        struct Plane {
            position: vec3f,
            normal: vec3f,
            material: Material
        };


        @vertex
        fn main_vs(@builtin(vertex_index) VertexIndex : u32) -> VSOut {
            // Origin in bottom left corner (-1, -1). TODO: Move to js, the same for every vert
            const pos = array<vec2f, 4>(
                vec2f(-1.0, 1.0), 
                vec2f(-1.0, -1.0), 
                vec2f(1.0, 1.0), 
                vec2f(1.0, -1.0)
            );
            
            return VSOut(
                vec4f(pos[VertexIndex], 0.0, 1.0),
                pos[VertexIndex]
            );
        }

        fn get_camera_ray(uv: vec2f) -> Ray {
            // Create camera frame - following the procedure outlined in the book p.86
            let v: vec3f = normalize(uniforms.camera_look_point - uniforms.camera_position);
            let b1: vec3f = normalize(cross(v, uniforms.camera_up_vector));
            let b2: vec3f = cross(b1, v);

            return Ray(
                uniforms.camera_position,
                b1 * uv.x + b2 * uv.y + v * uniforms.camera_const,
                TMIN,
                TMAX
            );
        }

        fn intersect_sphere(ray: ptr<function, Ray>, sphere: Sphere, spherical_harmonics: array<vec3f, 16>, hit: ptr<function, HitInfo>) -> bool {            
            let o_to_sphere: vec3f = (*ray).origin - sphere.center;

            let a: f32 = dot((*ray).direction, (*ray).direction);
            let b: f32 = 2 * dot(o_to_sphere, (*ray).direction);
            let c: f32 = dot(o_to_sphere, o_to_sphere) - (sphere.radius * sphere.radius);

            let d: f32 = b * b - 4 * a * c;
            
            if (d < 0 ) { return false; }

            
            let t1: f32 = (-b - sqrt(d)) / (2.0 * a);
            let t2: f32 = (-b + sqrt(d)) / (2.0 * a);
            var t = min(t1,t2);
            if (t < (*ray).t_min || (*ray).t_max < t) {
                return false;
            }

            // Update the hit info
            (*hit).has_hit = true;
            (*hit).dist = t;
            (*hit).entry = (*ray).origin + (*ray).direction * t;
            (*hit).exit = (*ray).origin + (*ray).direction * max(t1,t2);
            (*hit).material = Material();
            (*hit).material.color = compute_color_from_sh((*hit).entry, spherical_harmonics);
            (*hit).normal = normalize((*hit).entry - sphere.center);
            
            return true;
        }


        fn intersect_bsp_tree(ray: ptr<function, Ray>, intersections: ptr<function, array<HitInfo, MAXINTERSECTIONS>>, n_intersections: ptr<function, i32>) -> bool {
            var branch_lvl: u32 = 0;
            var near_node: u32 = 0;
            var far_node: u32 = 0;
            var t = 0.0;
            var node: u32 = 0;
            
            for (var i: u32 = 0; i <= MAX_LEVEL; i++) {
                let tree_node = bsp_tree[node];
                let node_axis_leaf = tree_node.x&3u;
                if (node_axis_leaf == BSP_LEAF) {
                    
                    // A leaf was found
                    let node_count = tree_node.x>>2u;
                    let node_id = tree_node.y;
                    var found = false;

                    for (var j: u32 = 0; j < node_count; j++) {
                        let obj_idx = tree_ids[node_id + j];
                        var hit = HitInfo();
                        if (intersect_ellipsoid(ray, obj_idx, &hit)) {
                            (*ray).t_max = hit.dist;
                            (*intersections)[(*n_intersections)] = hit;
                            (*n_intersections) += 1;
                            if (MAXINTERSECTIONS <= (*n_intersections)) {
                                return true;
                            }
                        }
                    }

                    if (branch_lvl == 0u) {
                        return true;
                    } else {
                        branch_lvl--;
                        i = branch_node[branch_lvl].x;
                        node = branch_node[branch_lvl].y;
                        (*ray).t_min = branch_ray[branch_lvl].x;
                        (*ray).t_max = branch_ray[branch_lvl].y;
                        continue;
                    }
                }

                let axis_direction = (*ray).direction[node_axis_leaf]; 
                let axis_origin = (*ray).origin[node_axis_leaf]; 
                if (0.0 <= axis_direction) {
                    near_node = tree_node.z;    // left
                    far_node = tree_node.w;     // right
                } else {
                    near_node = tree_node.w;    // right
                    far_node = tree_node.z;     // left
                }
                let node_plane = bsp_planes[node];
                let denom = select(axis_direction, 1.0e-8f, abs(axis_direction) < 1.0e-8f); 
                t = (node_plane - axis_origin)/denom;
                if (t > (*ray).t_max) { 
                    node = near_node; 
                } else if (t < (*ray).t_min) { 
                    node = far_node; 
                } else {
                    branch_node[branch_lvl].x = i; 
                    branch_node[branch_lvl].y = far_node; 
                    branch_ray[branch_lvl].x = t; 
                    branch_ray[branch_lvl].y = (*ray).t_max; 
                    branch_lvl++;
                    (*ray).t_max = t;
                    node = near_node; 
                }
            }
            return true;
        }
        

        fn quaternion_to_matrix(q: vec4f) -> mat3x3<f32> {
            // See: "Quaternion-derived rotation matrix" https://en.wikipedia.org/wiki/Quaternions_and_spatial_rotation 
            let xx = q.x * q.x;
            let yy = q.y * q.y;
            let zz = q.z * q.z;
            let xy = q.x * q.y;
            let xz = q.x * q.z;
            let yz = q.y * q.z;
            let wx = q.w * q.x;
            let wy = q.w * q.y;
            let wz = q.w * q.z;

            return mat3x3<f32>(
                vec3<f32>(1.0 - 2.0 * (yy + zz), 2.0 * (xy - wz), 2.0 * (xz + wy)),
                vec3<f32>(2.0 * (xy + wz), 1.0 - 2.0 * (xx + zz), 2.0 * (yz - wx)),
                vec3<f32>(2.0 * (xz - wy), 2.0 * (yz + wx), 1.0 - 2.0 * (xx + yy))
            );
        }

        fn rotate_vector(v: vec3<f32>, rotation_matrix: mat3x3<f32>) -> vec3<f32> {
            return rotation_matrix * v;
        }


        // Function to calculate Gaussian value at a point
        fn gaussian_value(point: vec3f, center: vec3f, scales: vec3f, sigma: vec3f, amplitude: f32) -> f32 {
            let diff = point - center;
            let scaled_diff = diff / sigma;
            let distance_squared = length(scaled_diff) * length(scaled_diff); 
            return amplitude * exp(-distance_squared / 2.0);
        }

        fn sample_gaussian(gaussian: Gaussian, point: vec3f) -> f32 {
            let rot_matrix: mat3x3f = quaternion_to_matrix(gaussian.rotation);
            let translated_point: vec3<f32> = point - gaussian.position.xyz;
            let rotated_point: vec3<f32> = rot_matrix * translated_point;
            let scaled_point: vec3<f32> = rotated_point / gaussian.scale;

            let exponent: f32 = -0.5 * dot(scaled_point, scaled_point);
            return exp(exponent);
        }
        // def gaussian_3d(scale, rotation, position, point):
        //     """ Sample a 3D Gaussian defined by scale, rotation, and position at a given point. """
        //     # Convert quaternion to rotation matrix
        //     rot_matrix = quaternion_to_matrix(rotation)

        //     # Apply the inverse rotation to the point
        //     rotated_point = np.dot(rot_matrix.T, point - position[:3])

        //     # Calculate the Gaussian
        //     power = -0.5 * np.sum((rotated_point / scale) ** 2)
        //     return math.exp(power)

        fn calculate_alpha(hit: HitInfo, sd:f32, amp: f32) -> f32 {

            // Gaussian function parameters
            let sigma: vec3f = hit.gaussian.scale * 4.0 / sd; // would really like to get rid of the extra constant here, its just an arbirary number beacuase it looks nicer
            let amplitude: f32 = amp * hit.material.color.a;

            // Integrate the Gaussian function along the ray path inside the ellipsoid
            let step_count: i32 = 10;
            let step_vector: vec3f = (hit.exit - hit.entry) / f32(step_count);
            var total_opacity: f32 = 0.0;
            var current_point: vec3f = hit.entry;

            for (var i: i32 = 0; i < step_count; i++) {
                total_opacity += sample_gaussian(hit.gaussian, current_point);//gaussian_value(current_point, hit.gaussian.position, hit.gaussian.scale, sigma, amplitude);
                current_point += step_vector;
            }
            total_opacity /= f32(step_count);

            // Normalize and clamp opacity
            let normalized_opacity = max(min(total_opacity, 1),0);

            return normalized_opacity;
        }

        fn inverse_rotate_vector(v: vec3<f32>, q: vec4<f32>) -> vec3<f32> {
            let rotationMatrix = quaternion_to_matrix(q);
            let inverseRotationMatrix = transpose(rotationMatrix);
            return inverseRotationMatrix * v;
        }

        fn rotateVector(v: vec3<f32>, q: vec4<f32>) -> vec3<f32> {
            let rotationMatrix = quaternion_to_matrix(q);
            return rotationMatrix * v;
        }

        fn calculate_rotated_normal(hit_pos: vec3<f32>, gaussian: Gaussian) -> vec3<f32> {
            // Calculate the rotation matrix from the quaternion
            let rotation_matrix = quaternion_to_matrix(gaussian.rotation);
            
            // Inverse rotation is the transpose of the rotation matrix
            let inverse_rotation_matrix = transpose(rotation_matrix);
            
            // Transform the hit position into the local space of the ellipsoid
            let local_hit_pos = rotate_vector(hit_pos - gaussian.position, inverse_rotation_matrix);
            
            // Calculate the normal in local space
            let local_normal = normalize(vec3(
                local_hit_pos.x / (gaussian.scale.x * gaussian.scale.x),
                local_hit_pos.y / (gaussian.scale.y * gaussian.scale.y),
                local_hit_pos.z / (gaussian.scale.z * gaussian.scale.z)
            ));
            
            // Rotate the normal back to world space
            let world_normal = rotate_vector(local_normal, rotation_matrix);
            
            return world_normal;
        }  

        fn intersect_ellipsoid(ray: ptr<function, Ray>, id: u32, hit: ptr<function, HitInfo>) -> bool {            
            let gaussian: Gaussian = gaussians[id];
            let spherical_harmonics: array<vec3f,16> = spherical_harmonics[id];

            let translated_ray_origin = (*ray).origin - gaussian.position;
            
            // Apply inverse rotation to the ray to align with the ellipsoid's axes
            let rotated_ray_origin = inverse_rotate_vector(translated_ray_origin, gaussian.rotation);
            let rotated_ray_direction = inverse_rotate_vector((*ray).direction, gaussian.rotation);

            // Scale the ray for the ellipsoid's local space
            let origin_normalized = rotated_ray_origin / gaussian.scale;
            let direction_normalized = rotated_ray_direction / gaussian.scale;

            // Check if the ray originates inside the ellipsoid
            if (dot(origin_normalized, origin_normalized) < 1.0) {
                return false;
            }

            let a = dot(direction_normalized, direction_normalized);
            let b = dot(origin_normalized, direction_normalized);
            let c = dot(origin_normalized, origin_normalized);
            let discriminant = b * b - a * (c - 1.0);
            if (discriminant < 0.0) {
                return false;
            }

            let sqrt_discriminant = sqrt(discriminant);
            let t1 = (-b - sqrt_discriminant) / a;
            let t2 = (-b + sqrt_discriminant) / a;
            let t = min(t1,t2);

            if (t < (*ray).t_min) {//|| (*ray).t_max < max(t1,t2)) {
                return false;
            }

            (*hit).gaussian = gaussian;
            (*hit).entry = (*ray).origin + (*ray).direction * t;
            (*hit).exit = (*ray).origin + (*ray).direction * max(t1,t2);
            (*hit).normal = calculate_rotated_normal((*hit).entry, gaussian);
            (*hit).dist = t;
            (*hit).material = Material();
            (*hit).material.color = compute_color_from_sh((*hit).entry, spherical_harmonics);
            (*hit).material.color.a = (*hit).gaussian.normal.a; // alpha is stored in normal for now, works 

            return true;
        }

        fn intersect_test(ray: ptr<function, Ray>, gaussian: Gaussian, color: vec4f, hit: ptr<function, HitInfo>) -> bool {
            let translated_ray_origin = (*ray).origin - gaussian.position;
            
            // Apply inverse rotation to the ray to align with the ellipsoid's axes
            let rotated_ray_origin = inverse_rotate_vector(translated_ray_origin, gaussian.rotation);
            let rotated_ray_direction = inverse_rotate_vector((*ray).direction, gaussian.rotation);

            // Scale the ray for the ellipsoid's local space
            let origin_normalized = rotated_ray_origin / gaussian.scale;
            let direction_normalized = rotated_ray_direction / gaussian.scale;

            // Check if the ray originates inside the ellipsoid
            if (dot(origin_normalized, origin_normalized) < 1.0) {
                return false;
            }

            let a = dot(direction_normalized, direction_normalized);
            let b = dot(origin_normalized, direction_normalized);
            let c = dot(origin_normalized, origin_normalized);
            let discriminant = b * b - a * (c - 1.0);
            if (discriminant < 0.0) {
                return false;
            }

            let sqrt_discriminant = sqrt(discriminant);
            let t1 = (-b - sqrt_discriminant) / a;
            let t2 = (-b + sqrt_discriminant) / a;
            let t = min(t1,t2);

            if (t < (*ray).t_min) {//|| (*ray).t_max < max(t1,t2)) {
                return false;
            }

            (*hit).gaussian = gaussian;
            (*hit).entry = (*ray).origin + (*ray).direction * t;
            (*hit).exit = (*ray).origin + (*ray).direction * max(t1,t2);
            (*hit).normal = calculate_rotated_normal((*hit).entry, gaussian);
            (*hit).dist = t;
            (*hit).material = Material();
            (*hit).material.color = color;

            return true;
        }
        
        // PLEASE NOTE: This function is copied from: https://github.com/cvlab-epfl/gaussian-splatting-web/blob/905b3c0fb8961e42c79ef97e64609e82383ca1c2/src/shaders.ts#L1
        fn compute_color_from_sh(position: vec3f, sh: array<vec3f, 16>) -> vec4f {
            let dir = normalize(position - uniforms.camera_position);
            var result = SH_C0 * sh[0];

            // if deg > 0
            let x = dir.x;
            let y = dir.y;
            let z = dir.z;

            result = result + SH_C1 * (-y * sh[1] + z * sh[2] - x * sh[3]);

            let xx = x * x;
            let yy = y * y;
            let zz = z * z;
            let xy = x * y;
            let xz = x * z;
            let yz = y * z;

            // if (sh_degree > 1) {
            result = result +
                SH_C2[0] * xy * sh[4] +
                SH_C2[1] * yz * sh[5] +
                SH_C2[2] * (2. * zz - xx - yy) * sh[6] +
                SH_C2[3] * xz * sh[7] +
                SH_C2[4] * (xx - yy) * sh[8];
            
            // if (sh_degree > 2) {
            result = result +
                SH_C3[0] * y * (3. * xx - yy) * sh[9] +
                SH_C3[1] * xy * z * sh[10] +
                SH_C3[2] * y * (4. * zz - xx - yy) * sh[11] +
                SH_C3[3] * z * (2. * zz - 3. * xx - 3. * yy) * sh[12] +
                SH_C3[4] * x * (4. * zz - xx - yy) * sh[13] +
                SH_C3[5] * z * (xx - yy) * sh[14] +
                SH_C3[6] * x * (xx - 3. * yy) * sh[15];

            // unconditional
            result = result + 0.5;

            return max(vec4f(result,1), vec4f(0.));
        }

        fn intersect_min_max(ray: ptr<function, Ray>) -> bool {
            let p1 = (aabb.min - (*ray).origin) / (*ray).direction;
            let p2 = (aabb.max - (*ray).origin) / (*ray).direction;
            let p_min = min(p1, p2);
            let p_max = max(p1, p2);
            let t_min = max(p_min.x, max(p_min.y, p_min.z));
            let t_max = min(p_max.x, min(p_max.y, p_max.z));
            if (t_max < t_min || (*ray).t_max < t_min || t_max < (*ray).t_min) {
                return false;
            }
            (*ray).t_min = max(t_min - uniforms.eps, (*ray).t_min);
            (*ray).t_max = min(t_max + uniforms.eps, (*ray).t_max);
            return true;
        }

        fn intersect_scene(ray: ptr<function, Ray>) -> vec4f {
            var intersections = array<HitInfo, MAXINTERSECTIONS>();
            var n_intersections: i32 = 0;

            var accumulated_color: vec3f = BACKGROUNDCOLOR.rgb;
            var accumulated_alpha: f32 = 1.0;
            
            // Scene description for debugging simple ellipsoids
            let default_material: Material = Material();
            var red = default_material;
            var blue = default_material;
            var green = default_material;
            red.color = vec4f(1.0, 0.0, 0.0, 1);
            blue.color = vec4f(0.0, 0.0, 1.0, 1);
            green.color = vec4f(0.0, 1.0, 0.0, 1);

            var redg: Gaussian = Gaussian(
                vec3f(0,0,0),   // pos
                vec3f(1,1,1),   // scale
                vec4f(0,0,0,1), // rotation
                vec4f(1,1,1, 0),   // normal
            );
            var blueg: Gaussian = Gaussian(
                vec3f(0,1,0),   // pos
                vec3f(1,1,1),   // scale
                vec4f(0,0,0,1),
                vec4f(1,1,1, 0),   // normal
            );
            var greeng: Gaussian = Gaussian(
                vec3f(0,0.5,0.87),   // pos
                vec3f(1,1,1),   // scale
                vec4f(0,0,0,1),     // rotation
                vec4f(1,1,1, 0),   // normal
            );
            var g = array<Gaussian, 3>(blueg, redg, greeng);
            var m = array<Material, 3>( blue, red, green);
            // Testing of hard coded ellipsoids / spheres
            for (var i: u32 = 0; i < 1; i++) {
                var hit = HitInfo();
                if (intersect_test(ray, g[i], m[i].color, &hit)) {
                    intersections[n_intersections] = hit;
                    n_intersections += 1;
                }
            }

            // if (intersect_min_max(ray)) {
            //     // Remove the gaussians too close by
            //     (*ray).t_min = 1;
            //     intersect_bsp_tree(ray, &intersections, &n_intersections);
            // }
            
            
            // Alpha-blending
            for (var i: i32 = n_intersections - 1; 0 <= i; i--) {
                let hit = intersections[i];
                let hit_opacity = calculate_alpha(hit, 4, 3); // 4,3 er nice

                accumulated_color = mix(accumulated_color, hit.material.color.rgb, hit_opacity);
                accumulated_alpha *= (1.0 - hit_opacity);
            }

            return vec4<f32>(accumulated_color, accumulated_alpha);
        }

        @fragment
        fn main_fs(@builtin(position) fragcoord: vec4f, @location(0) coords: vec2f) -> FSOut {
            let launch_idx: u32 = u32(fragcoord.y) * u32(uniforms.canvas_width) + u32(fragcoord.x); 
            var seed: u32 = tea(launch_idx, u32(uniforms.frame_number));

            let uv = vec2f(coords.x * uniforms.aspect_ratio * 0.5, coords.y * 0.5);

            var ray = get_camera_ray(uv);
            var shaded_result: vec4f = intersect_scene(&ray);
            
            let a = gaussians[0];
            let b = spherical_harmonics[0];
            var e = aabb;
            var f = tree_ids[0];
            var g = bsp_tree[0];
            var h = bsp_planes[0];

            let curr_sum: vec3f = textureLoad(renderTexture, vec2u(fragcoord.xy), 0).rgb * f32(uniforms.frame_number); 
            var accum_color = (shaded_result.rgb + curr_sum)/(uniforms.frame_number + 1);

            return FSOut(
                // vec4f(pow(accum_color, vec3f(1.0/uniforms.gamma)), shaded_result.a),
                shaded_result,
                vec4f(vec3f(), 1.0)
            );
        }


        // PRNG xorshift seed generator by NVIDIA
        fn tea(val0: u32, val1: u32) -> u32 {
            const N = 16u; // User specified number of iterations
            var v0 = val0;
            var v1 = val1;
            var s0 = 0u;
            for (var n = 0u; n < N; n++) {
                s0 += 0x9e3779b9;
                v0 += ((v1<<4)+0xa341316c)^(v1+s0)^((v1>>5)+0xc8013ea4);
                v1 += ((v0<<4)+0xad90777d)^(v0+s0)^((v0>>5)+0x7e95761e);
            }
            return v0;
        }

        // Generate random unsigned int in [0, 2^31)
        fn mcg31(prev: ptr<function, u32>) -> u32 {
            const LCG_A = 1977654935u; // Multiplier from Hui-Ching Tang [EJOR 2007] 
            *prev = (LCG_A * (*prev)) & 0x7FFFFFFF;
            return *prev;
        }
        // Generate random float in [0, 1)
        fn random(prev: ptr<function, u32>) -> f32 {
            return f32(mcg31(prev)) / f32(0x80000000); 
        }


    </script>
</head>
<body>
    <input type="file" id="file-input" /><br>
    <pre id="file-content"></pre>

    <p style="visibility: hidden;" id="loading-text"> ... Please wait ... </p>
    <canvas id="webgpu-canvas" width=512 height=512>
        Error: Browser doesn't support HTML5 canvas
    </canvas>

    <!-------- Interface -------->
    <table>
        <!-- FPS Counter -->
        <tr>
            <td colspan="2">
                <label id="fps-label">FPS: --</label>
            </td>
        </tr>
        <!-- Zoom -->
        <tr>
            <td colspan="2">
                <!-- NOTE: oninput not supported in IE10, but nobody uses it and oninput gives nice smooth slide -->
                <input type="range" id="zoom-slider" name="zoom-slider" min="0.1" max="10" step ="0.1" value="3." list="markers" oninput=onZoomSliderChange(this.value)>
                <label id="zoom-label">Zoom: 1</label>
            </td>
        </tr>
        <!-- Update -->
        <tr>
            <td colspan="2">
                <input type="checkbox" id="progressive-update-checkbox" name="progressive-update-checkbox" onchange=onDoProgressiveUpdatesChange(this.checked)>
                <label for="progressive-update-checkbox">Use progressive updating</label>
            </td>
        </tr>
    </table>
</body>
</html>